import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

group 'com.mohistmc.kite'
def psVersion = version = System.getenv('RELEASE_VERSION') ?: '0.0.0-DEV'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
    implementation 'org.spongepowered:mixin:0.8.5'
    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation "org.ow2.asm:asm:${project.asm_version}"
    implementation "org.ow2.asm:asm-analysis:${project.asm_version}"
    implementation "org.ow2.asm:asm-commons:${project.asm_version}"
    implementation "org.ow2.asm:asm-tree:${project.asm_version}"
    implementation "org.ow2.asm:asm-util:${project.asm_version}"
}

jar {
    manifest {
        def sdf = new SimpleDateFormat('EEE, MMM d, yyyy hh:mm:ss a z', Locale.US)
        sdf.setTimeZone(TimeZone.getTimeZone('GMT'))
        attributes 'Implementation-Title': 'MohistKite'
        attributes 'Implementation-Version': psVersion
        attributes 'Implementation-Vendor': sdf.format(new Date())
        attributes 'Can-Redefine-Classes': 'true'
        attributes 'Can-Retransform-Classes': 'true'
        attributes 'Agent-Class': 'com.mohistmc.kite.MohistKiteAgent'
        attributes 'Premain-Class': 'com.mohistmc.kite.MohistKiteAgent'
        attributes 'Main-Class': 'com.mohistmc.kite.launcher.Launcher'
    }
}

shadowJar {
    relocate 'com.google.common', 'com.mohistmc.kite.guava'
    archiveClassifier.set('')
}

build.dependsOn(shadowJar)
